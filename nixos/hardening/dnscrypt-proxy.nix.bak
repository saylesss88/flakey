{
  pkgs,
  lib,
  config,
  ...
}:
let
  hasIPv6Internet = true;
  StateDirName = "dnscrypt-proxy";
  StatePath = "/var/lib/${StateDirName}";
  blocklistDir = "${StatePath}/blocklists";
in
{
  networking = {
    nameservers = [
      "127.0.0.1"
      "::1"
    ];
    networkmanager.dns = "none";
  };

  services.resolved.enable = lib.mkForce false;

  environment.etc."dnscrypt-proxy/blocklists/sources.txt".text = ''
    https://raw.githubusercontent.com/nextdns/cname-cloaking-blocklist/master/domains
    https://adguardteam.github.io/AdGuardSDNSFilter/Filters/filter.txt
    https://big.oisd.nl/domainswild
    https://raw.githubusercontent.com/hagezi/dns-blocklists/main/wildcard/pro-onlydomains.txt
    https://raw.githubusercontent.com/hagezi/dns-blocklists/main/wildcard/tif-onlydomains.txt
  '';

  systemd.services.dnscrypt-filterlist-update = {
    description = "DNSCrypt Filterlist Update";
    wantedBy = [ "multi-user.target" ];
    requires = [ "network-online.target" ];
    after = [ "network-online.target" ];
    serviceConfig = {
      Type = "oneshot";
      User = "root";
      StateDirectory = StateDirName;
      ExecStart = "${pkgs.bash}/bin/bash -c ''
        mkdir -p ${blocklistDir}
        cd ${blocklistDir}
        touch domains-allowlist.txt
        rm -f temp.txt merged.txt
        > temp.txt
        echo 'Fetching blocklists...'
        while read -r url; do
          ${pkgs.curl}/bin/curl -sfL --max-time 30 \"$url\" >> temp.txt 2>/dev/null || echo \"Failed to fetch $url\"
        done < /etc/dnscrypt-proxy/blocklists/sources.txt
        ${pkgs.coreutils}/bin/sort -u temp.txt > merged.txt
        ${pkgs.dnscrypt-proxy}/bin/generate-domains-blocklist \
          -b merged.txt \
          -a domains-allowlist.txt \
          -o blocklist.txt
        chmod 644 blocklist.txt domains-allowlist.txt
        rm -f temp.txt merged.txt
        ${pkgs.systemd}/bin/systemctl --no-block try-reload-or-restart dnscrypt-proxy.service
      ''";
    };
  };

  systemd.timers.dnscrypt-filterlist-update = {
    description = "Run 15min after boot and every 5h";
    wantedBy = [ "timers.target" ];
    timerConfig = {
      OnBootSec = "15min";
      OnUnitActiveSec = "5h";
      Persistent = true;
    };
  };

  services.dnscrypt-proxy = {
    enable = true;
    settings = {
      bootstrap_resolvers = [
        "1.1.1.1:53"
        "1.0.0.1:53"
      ];
      sources.public-resolvers = {
        urls = [
          "https://raw.githubusercontent.com/DNSCrypt/dnscrypt-resolvers/master/v3/public-resolvers.md"
          "https://download.dnscrypt.info/resolvers-list/v3/public-resolvers.md"
        ];
        minisign_key = "RWQf6LRCGA9i53mlYecO4IzT51TGPpvWucNSCh1CBM0QTaLn73Y7GFO3";
        cache_file = "${StatePath}/public-resolvers.md";
      };
      sources.relays = {
        urls = [
          "https://raw.githubusercontent.com/DNSCrypt/dnscrypt-resolvers/master/v3/relays.md"
          "https://download.dnscrypt.info/resolvers-list/v3/relays.md"
        ];
        cache_file = "${StatePath}/relays.md";
        minisign_key = "RWQf6LRCGA9i53mlYecO4IzT51TGPpvWucNSCh1CBM0QTaLn73Y7GFO3";
      };
      sources.odoh-servers = {
        urls = [
          "https://raw.githubusercontent.com/DNSCrypt/dnscrypt-resolvers/master/v3/odoh-servers.md"
          "https://download.dnscrypt.info/resolvers-list/v3/odoh-servers.md"
        ];
        cache_file = "${StatePath}/odoh-servers.md";
        minisign_key = "RWQf6LRCGA9i53mlYecO4IzT51TGPpvWucNSCh1CBM0QTaLn73Y7GFO3";
      };
      sources.odoh-relays = {
        urls = [
          "https://raw.githubusercontent.com/DNSCrypt/dnscrypt-resolvers/master/v3/odoh-relays.md"
          "https://download.dnscrypt.info/resolvers-list/v3/odoh-relays.md"
        ];
        cache_file = "${StatePath}/odoh-relays.md";
        minisign_key = "RWQf6LRCGA9i53mlYecO4IzT51TGPpvWucNSCh1CBM0QTaLn73Y7GFO3";
      };
      server_names = [
        "odoh-cloudflare"
        "odoh-snowstorm"
      ];
      anonymized_dns = {
        skip_incompatible = true;
        routes = [
          {
            server_name = "odoh-snowstorm";
            via = [ "odohrelay-crypto-sx" ];
          }
          {
            server_name = "odoh-cloudflare";
            via = [ "odohrelay-crypto-sx" ];
          }
        ];
      };
      ipv6_servers = hasIPv6Internet;
      block_ipv6 = !hasIPv6Internet;
      blocked_names.blocked_names_file = "${blocklistDir}/blocklist.txt";
      require_dnssec = true;
      require_nolog = false;
      require_nofilter = false;
      odoh_servers = true;
      dnscrypt_servers = true;
    };
  };

  # Prevent restart during switch to avoid DNS loss during rebuilds
  systemd.services.dnscrypt-proxy = {
    restartIfChanged = false;
    reloadIfChanged = true;
    requires = [ "network-online.target" ];
    after = [ "network-online.target" ];
  };
}
